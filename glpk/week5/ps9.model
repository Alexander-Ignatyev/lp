# Problem Sets 5: Questions 6-9
# A county has 'n' towns.
# A corporation wishes to build supermarkets to serve the people in the county.
# For each town, the corporation can choose to or not to build a super market there.
# No town can have more than one super-market.
# We would like to minimize the number of super-markets built.
# Each town must have a super-market within a 'max_distance' minute driving time.
# The table 'distance_matrix' shows the driving times between the towns.

param n;

param max_distance;

param distance_matrix {i in 1..n, j in 1..n};

param mask_matrix {i in 1..n, j in 1..n} :=
    if distance_matrix[i, j] <= max_distance then 1 else 0;

var x {j in 1..n} binary;

minimize num_markets: sum {j in 1..n} x[j];
c{i in 1..n}: sum {j in 1..n}  mask_matrix[i,j]*x[j] >= 1;

solve;

display num_markets;
display x;
end;
